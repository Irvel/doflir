arr_size -> int;
arr_size = 10;
array -> int [arr_size];

define partition -> int (low -> int, high -> int) {
    i -> int;
    pivot -> int;
    temp -> int;

    i = low - 1;
    pivot = array[high];

    j -> int;
    j = low;
    while (j < high) {
        if (array[j] <= pivot) {
            i = i + 1;
            temp = array[i];
            array[i] = array[j];
            array[j] = temp;
        }
        j = j + 1;
    }

    temp = array[i + 1];
    array[i + 1] = array[high];
    array[high] = temp;
    return i + 1;
}

define quick_sort -> void (low -> int, high -> int) {
    pi -> int;
    if (low < high) {
        pi = partition(low, high);
        quick_sort(low, pi - 1);
        quick_sort(pi + 1, high);
    }
}

define print_array -> void () {
    idx -> int;
    idx = 0;
    while (idx < arr_size) {
        if (idx == arr_size - 1) {
            print(array[idx], " ");
        }
        else {
            print(array[idx], ", ");
        }
        idx = idx + 1;
    }
    println("", "");
}

define main {
    array = [3, 35, 2, 7, 85, 2, 11, 29, 4, 9];
    println("Array before sorting:");
    print_array();
    quick_sort(0, arr_size - 1);
    println("Array after sorting:");
    print_array();
    println("Reversed array:");
    array = array{f_reverse};
    print_array();

    print("Max val in array: ", array{f_max});
    println("");
    print("Min val in array: ", array{f_min});
    println("");
    print("Sum of the vals in array: ", array{f_sum});
    println("");
}
